plugins {
	id 'idea'
	id 'java-library'
	alias libs.plugins.mdg
	alias libs.plugins.lombok
	alias libs.plugins.modpublish
}

group = project.group
version = project.version + "-neoforge-rg+" + libs.versions.minecraft.get()

base.archivesName = project.archives_name

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		name = "Cjsah Maven"
		url = "https://server.cjsah.net:1002/maven/"
	}
}

dependencies {
	implementation(libs.rollinggate)
}

tasks.register('sourcesJar', Jar) {
	from delombok.outputs.files
	dependsOn delombok
	archiveClassifier = "sources"
}

apply from: "$rootDir/gradle/scripts/mdg.gradle"
apply from: "$rootDir/gradle/scripts/publish.gradle"

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

jar {
	from(project.file("LICENSE")) {
		rename { "${it}_${archives_name}" }
	}
}

lombok {
	version = "1.18.34"
}