plugins {
	alias libs.plugins.fabric.loom
	alias libs.plugins.modpublish
}

group = project.group
version = project.version

base.archivesName = project.archives_name

repositories {
	mavenCentral()
	maven {
		name "Masa Maven"
		url "https://masa.dy.fi/maven"
	}
}

loom {
	accessWidenerPath = file("src/main/resources/qca.accessWidener")
}

dependencies {
	minecraft libs.minecraft
	mappings libs.mappings.yarn

	compileOnly libs.lombok
	annotationProcessor libs.lombok

	modImplementation libs.bundles.fabric

	modImplementation libs.fabric.carpet
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName}"}
	}
}

publishMods {
	file = remapJar.archiveFile
	additionalFiles.from(remapSourcesJar.archiveFile)
	changelog = file("latest-changelog.md").text
	type = STABLE
	modLoaders.add("fabric")

	curseforge {
		projectId = "1166675"
		projectSlug = "qualia-carpet-addition"
		accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
		requires("fabric-api")
		requires("carpet")
		minecraftVersions.add("1.21.4")

	}
	modrinth {
		projectId = "JyI4tI00"
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		requires("fabric-api")
		requires("carpet")
		minecraftVersions.add("1.21.4")
	}
	github {
		repository = "QiuShui1012/Qualia-Carpet-Addition"
		accessToken = providers.environmentVariable("GITHUB_TOKEN")
		commitish = "fabric/1.21.4"
		displayName = project.version
		tagName = project.version
	}
}