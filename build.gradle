plugins {
	alias libs.plugins.fabric.loom
	alias libs.plugins.modpublish
}

group = libs.versions.group.get()
version = libs.versions.version.get()

base.archivesName = libs.versions.archives.name.get()

repositories {
	maven {
		name "Masa Maven"
		url "https://masa.dy.fi/maven"
	}
}

loom {
	accessWidenerPath = file("src/main/resources/qca.accessWidener")
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft libs.minecraft
	mappings libs.mappings.yarn

	compileOnly libs.lombok
	annotationProcessor libs.lombok

	modImplementation libs.fabric.loader

	modImplementation libs.fabric.api
	modImplementation libs.fabric.carpet
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName}"}
	}
}

publishMods {
	file = remapJar.archiveFile
	additionalFiles.from(remapSourcesJar.archiveFile)
	changelog = file("latest-changelog.md").text
	type = STABLE
	modLoaders.add("fabric")

	modrinth {
		accessToken = providers.environmentVariable("MODRINTH_PUBLISH_VERSION_PAT")
		projectId = "JyI4tI00"
		requires("carpet")
		minecraftVersions.add("1.21.2")
		minecraftVersions.add("1.21.3")
	}
	github {
		accessToken = providers.environmentVariable("GITHUB_PUBLISH_VERSION_PAT")
		repository = "QiuShui1012/Qualia-Carpet-Addition"
		displayName = project.version
		commitish = "main"
		tagName = project.version
	}
}