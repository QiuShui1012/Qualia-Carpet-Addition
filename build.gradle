plugins {
	id 'idea'
	alias libs.plugins.fabric.loom
	alias libs.plugins.lombok
	alias libs.plugins.modpublish
}

group = project.group
version = project.version + "-fabric+" + libs.versions.minecraft.get()

base.archivesName = project.archives_name

repositories {
	mavenCentral()
	maven {
		name "Masa Maven"
		url "https://masa.dy.fi/maven"
	}
}

loom {
	accessWidenerPath = file("src/main/resources/qca.accessWidener")
}

dependencies {
	minecraft libs.minecraft
	mappings loom.officialMojangMappings()

	modImplementation libs.bundles.fabric

	modImplementation libs.fabric.carpet
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName}"}
	}
}

apply from: "$rootDir/gradle/scripts/publish.gradle"

lombok {
	version = "1.18.34"
}

idea {
	module {
		downloadJavadoc = true
		downloadSources = true
	}
}